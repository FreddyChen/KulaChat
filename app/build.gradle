apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.freddy.kulachat"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias("KulaChat")
            keyPassword("1088299709")
            storeFile file("../kula_chat.jks")
            storePassword("1088299709")
        }

        debug {
            keyAlias("KulaChat")
            keyPassword("1088299709")
            storeFile file("../kula_chat.jks")
            storePassword("1088299709")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "LOG_DEBUG", "false")
            buildConfigField("String", "SERVER_URL", "\"https://wanandroid.com/\"")
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "LOG_DEBUG", "true")
            buildConfigField("String", "SERVER_URL", "\"https://wanandroid.com/\"")
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    "src/main/res/layouts/main",
                    "src/main/res/layouts/home",
                    "src/main/res/layouts",
                    "src/main/res"
            ]
            jniLibs.srcDirs = ['libs']
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def date = new Date().format("yyyyMMddHHmm")
            project.logger.debug("outputFile = ${outputFile}")
            project.logger.debug("buildTypeName = ${variant.buildType.name}")
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def buildType
                if (variant.buildType.name == "release") {
                    buildType = "release"
                } else if (variant.buildType.name == "debug") {
                    buildType = "debug"
                }
                if (buildType != null) {
                    outputFileName = "KulaChat_v${project.android.defaultConfig.versionName}.${project.android.defaultConfig.versionCode}_${buildType}_${date}.apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

greendao {
    schemaVersion 1// 数据库版本号
    daoPackage "${project.android.defaultConfig.applicationId}.database"// 设置包名
    targetGenDir 'src/main/java'
    // targetGenDirTest：设置生成单元测试目录
    // generateTests：设置自动生成单元测试用例
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'org.greenrobot:greendao:3.3.0'
    api 'com.google.dagger:dagger:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    api 'com.google.dagger:dagger-android:2.15'
    api 'com.google.dagger:dagger-android-support:2.15'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.15'
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.jaeger.statusbarutil:library:1.5.1'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation 'com.alibaba:fastjson:1.1.71.android'
    implementation 'com.github.ihsanbal:LoggingInterceptor:3.1.0-rc5'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation project(':ims_kula')
}
